import personajes.personaje_class.*
import hechizos.hechizo_class.*
import hechizos.logo_class.*
import hechizos.hechizos.*
import hechizos.libroDeHechizos_class.*
import fuerzaOscura.fuerzaOscura.*
import artefactos.artefactos.*
import artefactos.arma_class.*
import comerciantes.feriaDeHechiceria.*

describe "comercio" {

	const alacachulacachicomula = new Logo("alacachula cachicomula", 1)
	const xenia = new Personaje(hechizoPreferido = alacachulacachicomula)
	const thor = new Personaje(hechizoPreferido = hechizoBasico)
	const loki = new Personaje(hechizoPreferido = hechizoBasico, monedas = 5)
	const espada = new Arma(0)
	const armaduraSinRefuerzo = new Armadura(refuerzo = sinRefuerzo,peso=0)
	const cotaDeMalla = new CotaDeMalla(5)
	const armaduraConCotaDeMalla = new Armadura(refuerzo = cotaDeMalla,peso=0)
	const armaduraConBendicion = new Armadura(refuerzo = bendicion, poderDeLuchaBase = 2,peso=0)
	const armaduraConAlacachula = new Armadura(refuerzo = alacachulacachicomula, poderDeLuchaBase = 2,peso=0)
	const libroDeHechizos = new LibroDeHechizos()
	const espejo = new Espejo(0)

	test "Precio de hechizo de logo alacachulacachimula" {
		assert.equals(22,alacachulacachicomula.precio())
	}

	test "Monedas de xenia luego de canjear hechizo" {
		xenia.canjearHechizo(hechizoBasico)
		assert.equals(100,xenia.monedas())
	}

	test "Monedas de thor luego de canjear hechizo" {
		thor.canjearHechizo(alacachulacachicomula)
		assert.equals(83,thor.monedas())
	}

	test "Monedas de thor luego de canjear 2 veces el hechizo" {
		thor.canjearHechizo(hechizoBasico)
		thor.canjearHechizo(hechizoBasico)
		assert.equals(90,thor.monedas())
	}

	test "Loki quiere canjear el hechizo pero no le alcanzan las monedas" {
		assert.throwsExceptionWithMessage("No tiene monedas suficientes", { loki.canjearHechizo(alacachulacachicomula)})
	}

	test "Monedas de xenia luego de comprar artefactos " {
		xenia.adquirirArtefacto(espada, feriaDeHechiceria)
		xenia.adquirirArtefacto(collarDivino, feriaDeHechiceria)
		xenia.adquirirArtefacto(armaduraSinRefuerzo, feriaDeHechiceria)
		xenia.adquirirArtefacto(armaduraConCotaDeMalla, feriaDeHechiceria)
		assert.equals(85.5,xenia.monedas())
	}

	test "Cantidad de artefactos luego de hacer varias compras" {
		xenia.adquirirArtefacto(espada, feriaDeHechiceria)
		xenia.adquirirArtefacto(collarDivino, feriaDeHechiceria)
		xenia.adquirirArtefacto(armaduraSinRefuerzo, feriaDeHechiceria)
		xenia.adquirirArtefacto(armaduraConCotaDeMalla, feriaDeHechiceria)
		assert.equals(4,xenia.artefactos().size())
	}

	test "Monedas de thor luego de comprar artefactos" {
		thor.adquirirArtefacto(armaduraConBendicion, feriaDeHechiceria)
		thor.adquirirArtefacto(armaduraConAlacachula, feriaDeHechiceria)
		assert.equals(74,thor.monedas())
	}

	test "Loki quiere comprar el espejo, pero no le alcanza" {
		assert.throwsException({ loki.adquirirArtefacto(espejo)})
	}

	test "Xenia canjea por un libro de hechizos" {
		libroDeHechizos.agregarHechizo(alacachulacachicomula)
		libroDeHechizos.agregarHechizo(hechizoBasico)
		xenia.canjearHechizo(libroDeHechizos)
		assert.equals(69,xenia.monedas())
	}

}